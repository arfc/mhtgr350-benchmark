/*This input file defines a hexagonal elementary cell for the fuel compact.*/

/* Numbering:
* Universes: 1 to 10
* Surfaces: 100 to 200
* Cells: 500 to 600
*/

/******************************
*      Particle Definition    *
* Kernel radius 212.5e-6 m    *
* Buffer radius 312.5e-6 m    *
* IPyC radius   347.5e-6 m    *
* SiC radius    382.5e-6 m    *
* OPyC radius   422.5e-6 m    *
* Matrix Graphite             *
*******************************/
% -- Coated Particle
particle 1

uco    0.02125
buffer 0.03125
pyc    0.03475
sic    0.03825
pyc    0.04225
matrix

/*************************
*   Compact Definition   *
* Radius 0.6223 cm       *
* Length 4.928 cm        *
* Packing Fraction 0.35  *
**************************/

surf 100 inf
surf 101 inf
surf 102 inf

cell 500 2 matrix -100
cell 501 3 helium -101
cell 502 M block -102

pbed 5 2 "particles4.inp"

% -- Fuel Channel
surf 110 cyl 0.0 0.0 0.6223 0.0 4.928
surf 111 cyl 0.0 0.0 0.635  0.0 4.928
surf 112 hexxprism 0.0 0.0 0.9395 0.0 4.928

cell 510 0 fill 5  -110
cell 511 0 fill 3   110 -111
cell 512 0 fill M   111 -112
cell 513 0 outside  112

/**********************************
*            Materials            *
**********************************/

mat uco      -10.5        tmp 750                 rgb 255 255 0
 92235.06c   -0.1376
 92238.06c   -0.7504
  6000.06c   -0.0224
  8016.06c   -0.0896

mat buffer   -1.0         tmp 750 moder grph 6000 rgb 255 128   0
 6000.06c    -1.0000

mat pyc      -1.9         tmp 750 moder grph 6000 rgb 255   0   0
 6000.06c    -1.0000

mat sic      -3.2         tmp 750 moder grph 6000 rgb 153  76   0
14028.06c    -0.64446
14029.06c    -0.035025
14030.06c    -0.020915
 6000.06c    -0.2996

mat matrix   -1.75        tmp 750 moder grph 6000 rgb 160 160 160
 6000.06c    -1.0000

mat block    -1.85        tmp 750 moder grph 6000 rgb 155 255 232
 6000.06c    -1.0000

mat helium   -0.0041881   tmp 750                 rgb 150 105 255
 2004.06c    -1.0

/******************************
*            Input            *
******************************/

% --- Thermal scattering data for graphite:
therm grph 600 gr04.32t gr05.32t

% --- Cross section data library file path:
set acelib "/projects/sciteam/bbcc/serpent/xsdata/jeff312/sss_jeff312.xsdata"

% --- Periodic boundary condition:
set bc 3 % or 2? periodic or reflective?

% --- Neutron population and criticality cycles:
set pop 5000 100 40

% --- Geometry plot:
plot 3 1000 1000 0.2
plot 1 1000 1000 0.0

