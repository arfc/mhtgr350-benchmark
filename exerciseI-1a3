/*This input file defines a hexagonal elementary cell for the MHTGR fuel compact.*/

/* Numbering:
* Universes: 1 to 10
* Surfaces: 100 to 200
* Cells: 500 to 600
*/

/**************************
*   Geometry Definition   *
**************************/

surf 100 inf
surf 101 inf
surf 102 inf

cell 500 2 fuel   -100
cell 501 3 helium -101
cell 502 M block  -102

% -- Fuel Channel
surf 110 cyl 0.0 0.0 0.6225 0.0 4.928
surf 111 cyl 0.0 0.0 0.635  0.0 4.928
surf 112 hexxprism 0.0 0.0 0.9398 0.0 4.928

cell 510 0 fill 2  -110
cell 511 0 fill 3   110 -111
cell 512 0 fill M   111 -112
cell 513 0 outside  112

/**********************************
*            Materials            *
**********************************/

mat fuel     sum          tmp 600 moder grph2 6000 rgb 255 255 0
 92235.06c   1.58e-4
 92238.06c   8.47e-4
  8016.06c   1.51e-3
  6000.06c   7.00e-2
 14028.06c   2.85e-3
 14029.06c   1.40e-4
 14030.06c   8.96e-5

mat helium   sum          tmp 600                 rgb 150 105 255
 2004.06c    2.46e-5

mat block    sum          tmp 400 moder grph1 6000 rgb 155 255 232
 6000.05c    9.28e-2

/******************************
*            Input            *
******************************/
% --- Thermal scattering data for graphite:
therm grph1 400 gr01.32t
therm grph2 600 gr03.32t

% --- Cross section data library file path:
set acelib "/projects/sciteam/bbcc/serpent/xsdata/jeff312/sss_jeff312.xsdata"

% --- Periodic boundary condition:
set bc 3

% --- Neutron population and criticality cycles:
set pop 50000 100 40

% --- Geometry plot:
plot 3 1000 1000 0.2 -5 5 -5 5
plot 1 1000 1000 0.0 -5 5  0 5
